module minizinc/type

imports

  minizinc/common
  minizinc/expr

template options

  keyword -/- [A-Za-z0-9\_] // require layout after a keyword

context-free syntax

  MiniZinc-Type-Expr = MiniZinc-Base-Type-Expr
  MiniZinc-Type-Expr = MiniZinc-Array-Type-Expr
  
  MiniZinc-Base-Type-Expr.Type = <<MiniZinc-Var-Par?><MiniZinc-Opt-Type?><MiniZinc-Set-Type?><MiniZinc-Base-Type-Expr-Tail>>
  
  MiniZinc-Var-Par.Var = "var "
  MiniZinc-Var-Par.Par = "par "
  
  MiniZinc-Opt-Type.Opt = "opt "
  
  MiniZinc-Set-Type.Set = "set of "

  MiniZinc-Base-Type-Expr-Tail.Id    = MiniZinc-ID
  MiniZinc-Base-Type-Expr-Tail       = MiniZinc-Base-Type
  MiniZinc-Base-Type-Expr-Tail.Var   = MiniZinc-Type-Variable-Expr-Tail
  MiniZinc-Base-Type-Expr-Tail.Ann   = <ann>
  MiniZinc-Base-Type-Expr-Tail.Exprs = {MiniZinc-Expr ","}+ {avoid}
  MiniZinc-Base-Type-Expr-Tail.Range = <<MiniZinc-Expr>..<MiniZinc-Expr>>
  
  MiniZinc-Base-Type.Bool   = "bool"
  MiniZinc-Base-Type.Int    = "int"
  MiniZinc-Base-Type.Float  = "float"
  MiniZinc-Base-Type.String = "string"
  
  MiniZinc-Array-Type-Expr.Array = <array [<{MiniZinc-Type-Expr ","}+>] of <MiniZinc-Base-Type-Expr>>
  MiniZinc-Array-Type-Expr.List  = <list of <MiniZinc-Base-Type-Expr>>

lexical syntax

  MiniZinc-Type-Variable-Expr-Tail = [\$][A-Za-z][A-Za-z0-9\_]*

lexical restrictions

  MiniZinc-Type-Variable-Expr-Tail -/- [A-Za-z0-9\_]
